.include /asm51.h/

.area home (abs)
  nop
sjmp _main
.org 0x1000
.db 0x3f,0x06,0x5b,0x4f,0x66,0x6d,0x7d,0x07  ;将显示表存如地址0x1000
.db 0x7f,0x6f,0x77,0x7c,0x39,0x5e,0x79,0x71  ;数码管显示0~f的值

.area code (code)

;------------------------------------------------------------------------------
;主函数
;使数码管从右往左依次显示01234567
;------------------------------------------------------------------------------
_main:
01$:
  mov r4,#0x08
  mov r3,#0x00
02$:
  mov a,r3
  mov b,r3
  inc r3
  lcall _display
  lcall _delay1ms
  mov _P0,#0x00
  djnz r4,02$
  sjmp 01$

;延时函数
_delay1ms:      ;晶振12M
  mov r7,#10    ;外层循环10次
02$:
  mov r6,#50    ;内层循环50次
01$:
  djnz r6,01$
  djnz r7,02$
  ret
  ;总时间：1+（1+2x50）x10+2=,1.013ms

;------------------------------------------------------------------------------
;扫描显示函数                                                                  |
;使用A传递显示数字，B控制显示的数码管，                                          |
;如：DPL=1,DPH=1表示使第一(右往左数第2)个数码管显示1                             |
;------------------------------------------------------------------------------
_display:
  mov dptr,#0x1000
  movc a,@a+dptr
  mov r5,a                 ;读取表中数据
  mov a,b
  rl a
  rl a                      ;_P2_2口为3-8译码器的低位，左环移与之对应
  mov _P2,a
  mov _P0,r5
  ret
